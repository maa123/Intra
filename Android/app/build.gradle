apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

// See https://developer.android.com/studio/publish/app-signing#secure-shared-keystore
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
try {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (Exception ex) {
    logger.warn("No keys.  Release disabled: $ex.message")
    keystoreProperties['keyAlias'] = ''
    keystoreProperties['keyPassword'] = ''
    keystoreProperties['storeFile'] = '/dev/null'
    keystoreProperties['storePassword'] = ''
}

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        applicationId "app.intra"
        // Firebase Crashlytics requires SDK version 16.
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 60
        versionName "1.3.3"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    lintOptions {
        // Ignore lint errors that we believe are safe to ignore.
        baseline file("lint-baseline.xml")
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            signingConfig signingConfigs.config
            resValue("bool", "FIREBASE_ENABLED", "true")
            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir 'build/intermediates/stripped_native_libs/release/out/lib'
                unstrippedNativeLibsDir 'build/intermediates/merged_native_libs/release/out/lib'
            }
        }
        debug {
            pseudoLocalesEnabled true
            resValue("bool", "FIREBASE_ENABLED", "false")
            // Split build across multiple dex files.  This is necessary mostly because Guava adds
            // so many classes that we exceed the dex limit (65536 methods).  This does not apply
            // to release builds, where unused classes are pruned.
            multiDexEnabled true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        url 'test-repo'
    }
    maven {
        url '../gradle-plugin-repo'
    }
    maven {
        url '../firebase-repo'
    }
}

dependencies {
    implementation fileTree(include: [
            'guava-30.1-android.jar',
            'failureaccess-1.0.1.jar' // Dependency of Guava
    ], dir: 'libs')

    // Third-party test dependencies.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20171018'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    // UI libraries.
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    // For Firebase Analytics, etc.
    implementation 'com.google.firebase:firebase-core:18.0.2'
    implementation 'com.google.firebase:firebase-perf:19.1.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.3.1'
    implementation 'com.google.firebase:firebase-crashlytics-ndk:17.3.1'
    implementation 'com.google.firebase:firebase-config:20.0.3'
    // For go-tun2socks
    implementation project(":tun2socks")
}
// For Firebase Analytics
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.firebase-perf'
